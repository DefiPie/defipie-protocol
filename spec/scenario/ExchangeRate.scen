
Test "Initial Exchange Rate"
    NewController
    NewPToken ZRX pZRX initialExchangeRate:2e9
    Assert Equal (PToken pZRX ExchangeRateStored) (Exp 2e9)

Test "Initial Exchange Rate with Mint"
    NewController
    ListedPToken ZRX pZRX initialExchangeRate:5e9
    -- Check initial exchange holds
    Invariant Remains (PToken pZRX ExchangeRateStored) (Exp 5e9)
    -- Mint some tokens and verify invariant still holds
    Prep Geoff Some ZRX pZRX
    Mint Geoff 50e18 pZRX
    -- Also, verify token was minted at given exchange rate
    Assert Equal (Erc20 pZRX TotalSupply) (Exactly 10e9)
    Assert Equal (Erc20 pZRX TokenBalance Geoff) (Exactly 10e9)
    -- Let's mint again and verify it's still good.
    Prep Torrey Some ZRX pZRX
    Mint Torrey 50e18 pZRX
    -- Also, verify token was minted at given exchange rate
    Assert Equal (Erc20 pZRX TotalSupply) (Exactly 20e9)
    Assert Equal (Erc20 pZRX TokenBalance Torrey) (Exactly 10e9)

Test "ZRX: Exch. Rate:2e9, Cash(51e18) + Borrows(2.0e18) - Reserves(0.5e18) / Tokens(2.5e10)"
    NewController
    -- Decimals You=18, Decimals Us=8 -> 2e9 Exchange Rate
    ListedPToken ZRX pZRX initialExchangeRate:2e9 decimals:8
    Assert Equal (PToken pZRX ExchangeRateStored) (Exp 2e9)
    -- Mint 50.0 ZRX at given exchange rate
    Prep Geoff Some ZRX pZRX
    Assert Equal (Erc20 pZRX TotalSupply) Zero
    Mint Geoff 50e18 pZRX
    -- 50e18 / 2e9 = 2.5e10
    Assert Equal (Erc20 pZRX TotalSupply) (Exactly 2.5e10)
    Assert Equal (Erc20 pZRX TokenBalance Geoff) (Exactly 2.5e10)
    -- Set cash
    Erc20 ZRX Faucet pZRX 1.0e18
    Assert Equal (Erc20 ZRX TokenBalance pZRX) (Exactly 51.0e18)
    -- Mock total borrows
    PToken pZRX Mock totalBorrows 2.0e18
    Assert Equal (PToken pZRX TotalBorrows) (Exactly 2.0e18)
    -- Mock total reserves
    PToken pZRX Mock totalReserves 0.5e18
    Assert Equal (PToken pZRX Reserves) (Exactly 0.5e18)
    -- Okay, we're all set, let's check the exchange rate
    -- (51+2-0.5)e18/2.5e10 = 52.5e18/2.5e10 = 21e8 -> 21e26 (Exp)
    Assert Equal (PToken pZRX ExchangeRateStored) (Exp 2.1e9)

Test "USDC: Exch. Rate:2e-3, Cash(51e18) + Borrows(2.0e18) - Reserves(0.5e18) / Tokens(2.5e10)"
    NewController price:1000000000000.0 --1e18 and decimals factor (18 - 6) = 12
    -- Decimals You=6, Decimals Us=8 -> 2e-3 Exchange Rate
    ListedPToken USDC pUSDC initialExchangeRate:2e-3 decimals:8
    Assert Equal (PToken pUSDC ExchangeRateStored) (Exp 2e-3)
    -- Mint 50.0 USDC at given exchange rate
    Prep Geoff Little USDC pUSDC
    Assert Equal (Erc20 pUSDC TotalSupply) (Exactly 0.0)
    Mint Geoff 50e6 pUSDC
    -- 5.0e7 / 2e-3 = 2.5e10
    Assert Equal (Erc20 pUSDC TotalSupply) (Exactly 2.5e10)
    Assert Equal (Erc20 pUSDC TokenBalance Geoff) (Exactly 2.5e10)
    -- Set cash
    Erc20 USDC Faucet pUSDC 2.0e6
    Assert Equal (Erc20 USDC TokenBalance pUSDC) (Exactly 52.0e6)
    -- Mock total borrows
    PToken pUSDC Mock totalBorrows 5.0e6
    Assert Equal (PToken pUSDC TotalBorrows) (Exactly 5.0e6)
    -- Mock total reserves
    PToken pUSDC Mock totalReserves 1.5e6
    Assert Equal (PToken pUSDC Reserves) (Exactly 1.5e6)
    -- Okay, we're all set, let's check the exchange rate
    -- (52+5-1.5)e6/2.5e10 = 55.5e6/2.5e10 = 2.22e-3 -> 2.22e15 (Exp)
    Assert Equal (PToken pUSDC ExchangeRateStored) (Exp 2.22e-3)
