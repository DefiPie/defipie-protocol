-- Redeem Tests

Test "Mint then Redeem All"
    NewController
    ListedEtherToken pETH initialExchangeRate:0.005e9
    CallMintEth Geoff 0.005e18 pETH
    -- Check current affairs
    Assert Equal (Erc20 pETH TokenBalance Geoff) 10e8
    Assert Equal (PToken pETH UnderlyingBalance Geoff) 0.005e18
    Invariant Static (PToken pETH ExchangeRateStored)
    -- Now redeem after some time
    FastForward 2 Blocks
    Expect Changes (EtherBalance Geoff) +0.005e18
    RedeemEth Geoff 10e8 pETH
    Assert Equal (Erc20 pETH TokenBalance Geoff) Zero
    Assert Equal (PToken pETH UnderlyingBalance Geoff) Zero

Test "Mint then Redeem Part"
    NewController
    ListedEtherToken pETH initialExchangeRate:0.005e9
    CallMintEth Geoff 0.005e18 pETH
    -- Check current affairs
    Assert Equal (Erc20 pETH TokenBalance Geoff) 10e8
    Assert Equal (PToken pETH UnderlyingBalance Geoff) 0.005e18
    Invariant Static (PToken pETH ExchangeRateStored)
    -- Now redeem after some time
    FastForward 2 Blocks
    Expect Changes (EtherBalance Geoff) +0.001e18
    RedeemEth Geoff 2e8 pETH
    Assert Equal (Erc20 pETH TokenBalance Geoff) 8e8
    Assert Equal (PToken pETH UnderlyingBalance Geoff) 0.004e18

Test "Mint then Redeem Too Much"
    NewController
    ListedEtherToken pETH initialExchangeRate:0.005e9
    CallMintEth Geoff 0.005e18 pETH
    AllowFailures
    -- Check and hold static
    Invariant Static (PToken pETH ExchangeRateStored)
    Invariant Remains (Erc20 pETH TokenBalance Geoff) 10e8
    Invariant Static (EtherBalance Geoff)
    -- Now redeem after some time
    FastForward 2 Blocks
    RedeemEth Geoff 11e8 pETH
    -- TODO: This should really be REDEEM_NEW_ACCOUNT_BALANCE_CALCULATION_FAILED, but based on
    -- the order of subtractions, total supply comes before account supply.
    Assert Failure MATH_ERROR REDEEM_NEW_TOTAL_SUPPLY_CALCULATION_FAILED "3" -- TODO: This error is wrong

Test "Mint then Redeem Zero"
    NewController
    ListedEtherToken pETH initialExchangeRate:0.005e9
    CallMintEth Geoff 0.005e18 pETH
    -- Check and hold static
    Invariant Static (PToken pETH ExchangeRateStored)
    Invariant Remains (Erc20 pETH TokenBalance Geoff) 10e8
    Invariant Static (EtherBalance Geoff)
    -- Now redeem after some time
    FastForward 2 Blocks
    RedeemEth Geoff 0e9 pETH

Pending "Mint then redeem with interest - no reserves"
    Invariant Success
    NewController
    ListedPToken ZRX pETH initialExchangeRate:1e9
    Invariant Remains (PToken pETH Reserves) Zero
    Prep Geoff 50e18 ZRX pETH
    Mint Geoff 50e18 pETH
    Assert Equal (Erc20 pETH TokenBalance Geoff) 500e8
    Assert Equal (PToken pETH UnderlyingBalance Geoff) 50e18
    Assert Equal (PToken pETH ExchangeRate) 1e9
    -- Get some brah to borrow then repay
    BorrowAndRepayWithInterest ZRX pETH 10e18 5e18 interestRate:0.0001 blocks:5000
    -- We've accrued 10% interest for 5 blocks, or 50% of the amount,
    --   thus, we should have accrued 5e18 of interest for the protocol
    --   This is due pro-rata to all holders, but we just have one, so
    --   let's check that account is given correct new balance.
    Assert Equal (Erc20 pETH TokenBalance Geoff) 500e8
    Assert Equal (PToken pETH UnderlyingBalance Geoff) 55e18
    Assert Equal (PToken pETH ExchangeRate) 1.1e9
    -- Now redeem all with interest
    Redeem Geoff 500e8 pETH
    Assert Equal (Erc20 ZRX TokenBalance Geoff) 55e18
    Assert Equal (Erc20 ZRX TokenBalance pETH) 0e18
    Assert Equal (Erc20 pETH TokenBalance Geoff) 0e8
    Assert Equal (PToken pETH UnderlyingBalance Geoff) 0e18
    Assert Equal (PToken pETH ExchangeRate) 1e9

Pending "Mint then redeem part with interest - no reserves"
    Invariant Success
    NewController
    ListedPToken ZRX pETH initialExchangeRate:1e9
    Invariant Remains (PToken pETH Reserves) Zero
    Prep Geoff 50e18 ZRX pETH
    Mint Geoff 50e18 pETH
    Assert Equal (Erc20 pETH TokenBalance Geoff) 500e8
    Assert Equal (PToken pETH UnderlyingBalance Geoff) 50e18
    Assert Equal (PToken pETH ExchangeRate) 1e9
    -- Get some brah to borrow then repay
    BorrowAndRepayWithInterest ZRX pETH 10e18 5e18 interestRate:0.0001 blocks:5000
    -- We've accrued 10% interest for 5 blocks, or 50% of the amount,
    --   thus, we should have accrued 5e18 of interest for the protocol
    --   This is due pro-rata to all holders, but we just have one, so
    --   let's check that account is given correct new balance.
    Assert Equal (Erc20 pETH TokenBalance Geoff) 500e8
    Assert Equal (PToken pETH UnderlyingBalance Geoff) 55e18
    Assert Equal (PToken pETH ExchangeRate) 1.1e9
    -- Now redeem all with interest
    Redeem Geoff 499e8 pETH
    Assert Equal (Erc20 pETH TokenBalance Geoff) 1e8
    Assert Equal (PToken pETH UnderlyingBalance Geoff) 0.11e18
    Assert Equal (PToken pETH ExchangeRate) 1.1e9

Pending "Mint then redeem with reserves and interest"
    Invariant Success
    NewController
    ListedPToken ZRX pETH initialExchangeRate:1e9
    Prep Geoff 50e18 ZRX pETH
    Mint Geoff 50e18 pETH
    Assert Equal (Erc20 pETH TokenBalance Geoff) 500e8
    Assert Equal (PToken pETH UnderlyingBalance Geoff) 50e18
    Assert Equal (PToken pETH ExchangeRate) 1e9
    -- Get some brah to borrow then repay
    BorrowAndRepayWithInterest ZRX pETH 10e18 5e18 interestRate:0.0001 blocks:5000 reserveRate:0.2
    -- We've accrued 10% interest for 5 blocks, or 50% of the amount,
    --   thus, we should have accrued 5e18 of interest for the protocol
    --   The reserves should get 20% of this, or 1e18, and the rest
    --   is due pro-rata to all holders. We just have one, so
    --   let's check that account is given correct new balance.
    Assert Equal (Erc20 pETH TokenBalance Geoff) 500e8
    Assert Equal (PToken pETH Reserves) 1e18
    Assert Equal (PToken pETH UnderlyingBalance Geoff) 54e18
    -- 55e18 + 0e18 - 1e18 / 500
    Assert Equal (PToken pETH ExchangeRate) 1.08e9
    -- Now redeem all with interest
    Redeem Geoff 500e8 pETH
    Assert Equal (Erc20 ZRX TokenBalance Geoff) 54e18
    Assert Equal (Erc20 ZRX TokenBalance pETH) 1e18
    Assert Equal (Erc20 pETH TokenBalance Geoff) 0e8
    Assert Equal (PToken pETH Reserves) 1e18
    Assert Equal (PToken pETH UnderlyingBalance Geoff) 0e18
    Assert Equal (PToken pETH ExchangeRate) 1e9

Test "Two users Mint, one redeems"
    NewController
    ListedEtherToken pETH initialExchangeRate:0.005e9
    CallMintEth Geoff 0.002e18 pETH
    Assert Equal (Erc20 pETH TokenBalance Geoff) 4e8
    Assert Equal (Erc20 pETH TotalSupply) 4e8
    CallMintEth Torrey 0.004e18 pETH
    Assert Equal (Erc20 pETH TokenBalance Geoff) 4e8
    Assert Equal (Erc20 pETH TokenBalance Torrey) 8e8
    Assert Equal (Erc20 pETH TotalSupply) 12e8
    RedeemEth Torrey 3e8 pETH
    Assert Equal (Erc20 pETH TokenBalance Geoff) 4e8
    Assert Equal (Erc20 pETH TokenBalance Torrey) 5e8
    Assert Equal (Erc20 pETH TotalSupply) 9e8
