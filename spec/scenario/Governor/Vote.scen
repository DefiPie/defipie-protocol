
Macro GivenProposal
    Counter Deploy
    Timelock Deploy Jared 604800
    Pie Deploy Bank
    NewController
    ListedPPIEToken pPIE Pie
    From Bank (Pie Approve pPIE 10000000e18)
    From Bank (PToken pPIE Mint 10000000e18)
    Governor Deploy Governor LegitGov (Address Timelock) (Address RegistryProxy) Guardian
    Timelock SetAdmin (Address LegitGov)
    Enfranchise Root 100001e18
    Enfranchise Geoff 100
    Governor LegitGov Propose "Add 5" [(Address Counter)] [1] ["increment(uint256)"] [["0x5"]]
    MineBlock

Macro Enfranchise user amount
    From Bank (PToken pPIE Transfer user amount)
    From user (PToken pPIE Delegate user)

Test "Successfully Cast For Vote"
    GivenProposal
    From Geoff (Governor LegitGov Proposal LastProposal Vote For)
    Assert Log VoteCast (voter (Address Geoff)) (proposalId 1) (support true) (votes 100)
    Assert Equal (Governor LegitGov Proposal LastProposal ForVotes) 100
    Assert Equal (Governor LegitGov Proposal LastProposal AgainstVotes) 0
    Assert True (Governor LegitGov Proposal LastProposal HasVoted Geoff)

Test "Successfully Cast Against Vote"
    GivenProposal
    From Geoff (Governor LegitGov Proposal LastProposal Vote Against)
    Assert Log VoteCast (support false) (votes 100)
    Assert Equal (Governor LegitGov Proposal LastProposal ForVotes) 0
    Assert Equal (Governor LegitGov Proposal LastProposal AgainstVotes) 100
    Assert True (Governor LegitGov Proposal LastProposal HasVoted Geoff)

Test "Successfully Cast Zero Vote"
    GivenProposal
    From Torrey (Governor LegitGov Proposal LastProposal Vote For)
    Assert Log VoteCast (support true) (votes 0)
    Assert Equal (Governor LegitGov Proposal LastProposal ForVotes) 0
    Assert Equal (Governor LegitGov Proposal LastProposal AgainstVotes) 0
    Assert True (Governor LegitGov Proposal LastProposal HasVoted Torrey)

Test "Fail to vote twice"
    GivenProposal
    Governor LegitGov Proposal LastProposal Vote For
    AllowFailures
    Governor LegitGov Proposal LastProposal Vote For
    Assert Revert "revert Governor::_castVote: voter already voted"
    Governor LegitGov Proposal LastProposal Vote Against
    Assert Revert "revert Governor::_castVote: voter already voted"
    Assert Equal (Governor LegitGov Proposal LastProposal ForVotes) 100001e18
    Assert Equal (Governor LegitGov Proposal LastProposal AgainstVotes) 0
    Assert False (Governor LegitGov Proposal LastProposal HasVoted Geoff)

Test "Cannot vote before vote starts"
    GivenProposal
    AllowFailures
    SetBlockNumber 7
    Governor LegitGov Proposal LastProposal Vote For
    Assert Revert "revert Governor::_castVote: voting is closed"

Test "Cannot vote after vote ends"
    GivenProposal
    AllowFailures
    AdvanceBlocks 20000
    Governor LegitGov Proposal LastProposal Vote For
    Assert Revert "revert Governor::_castVote: voting is closed"

Test "Cannot vote on cancelled vote"
    GivenProposal
    From Guardian (Governor LegitGov Proposal LastProposal Cancel)
    AllowFailures
    Governor LegitGov Proposal LastProposal Vote For
    Assert Revert "revert Governor::_castVote: voting is closed"
